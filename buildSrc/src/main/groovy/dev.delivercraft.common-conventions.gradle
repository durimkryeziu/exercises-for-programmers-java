plugins {
    id 'java'
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()
            targets.configureEach {
                testTask.configure {
                    jvmArgs '-Djunit.jupiter.execution.parallel.enabled=true',
                            '-Djunit.jupiter.execution.parallel.mode.default=concurrent',
                            '-Djunit.jupiter.execution.parallel.mode.classes.default=concurrent',
                            '-Djunit.jupiter.execution.parallel.config.strategy=dynamic'
                }
            }
        }
    }
}

pmd {
    consoleOutput = true
    toolVersion = libs.versions.pmd.get()
    ruleSets = []
    ruleSetFiles = files(file("${rootProject.projectDir}/config/pmd/ruleset.xml"))
}

checkstyle {
    toolVersion = libs.versions.checkstyle.get()
}

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

tasks.check {
    dependsOn tasks.jacocoTestReport
}
